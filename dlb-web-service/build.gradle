plugins {
	id 'war'
	id 'org.springframework.boot' version '3.5.4'
}

group = 'com.dialoguebranch'
version = '1.2.5' // run updateVersion after changing this property

repositories {
	mavenCentral()
}

base {
	archivesName = "dlb-web-service"
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
	if (project.properties['buildEnv'] == null || !project.properties['buildEnv'].equals('dev')) {
		withJavadocJar()
		withSourcesJar()
	}
}

configurations {
	providedRuntime
}

dependencies {
	implementation 'org.apache.httpcomponents.client5:httpclient5:5.5'

	// Enabling this dependency will include the required dlb-core-java from local source.
	// The location of this project is defined in ../settings.gradle and assumes that it
	// is located at "../../dlb-core-java" (relative to this project).
	implementation project(':dlb-core-java')

	// Alternatively, import the dlb-core-java library through maven central
	//implementation 'com.dialoguebranch:dlb-core-java:1.2.3'

	// https://mvnrepository.com/artifact/io.jsonwebtoken/jjwt-api
	implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'

	// https://mvnrepository.com/artifact/javax.xml.bind/jaxb-api
	implementation 'javax.xml.bind:jaxb-api:2.4.0-b180830.0359'

	// https://mvnrepository.com/artifact/org.springframework/spring-context-support
	implementation 'org.springframework:spring-context-support:6.2.9'

	// https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-web
	implementation 'org.springframework.boot:spring-boot-starter-web:3.5.4'

	// https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-tomcat
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat:3.5.4'

	// https://mvnrepository.com/artifact/org.springdoc/springdoc-openapi-starter-webmvc-ui
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'

	implementation 'org.mariadb.jdbc:mariadb-java-client:3.5.4'
	implementation platform('org.hibernate.orm:hibernate-platform:7.0.9.Final')
	implementation 'org.hibernate.orm:hibernate-core'

	// Used for the Azure Data Lake Integration

		// https://mvnrepository.com/artifact/com.azure/azure-sdk-bom
		implementation 'com.azure:azure-sdk-bom:1.2.37'

		// https://mvnrepository.com/artifact/com.azure/azure-storage-file-datalake
		implementation 'com.azure:azure-storage-file-datalake:12.24.1'
}

configurations {
	runtime.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
	runtime.exclude group: 'org.slf4j', module: 'slf4j-jdk14'
	compile.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
	compile.exclude group: 'org.slf4j', module: 'slf4j-jdk14'
}

class UpdateConfigTask extends DefaultTask {
	private ProviderFactory providerFactory

	@Inject
	UpdateConfigTask(ProviderFactory providerFactory) {
		this.providerFactory = providerFactory
	}

	@TaskAction
	void doTaskAction() {
		Properties props = new Properties()
		def propPrefix = 'dlb-config'

		providerFactory.gradlePropertiesPrefixedBy(propPrefix).get().each { key, value ->
			if (!key.startsWith(propPrefix))
				return
			key = key.substring(propPrefix.length())
			key = key[0].toLowerCase() + key.substring(1)
			props[key] = value
		}

		// Add the current time as buildTime property
		props['buildTime'] = new Date().format("MM/dd/yyyy HH:mm:ss")

		def resDir = new File('src/main/resources')
		resDir.mkdirs()
		def out = new File(resDir, 'service.properties')
		out.withWriter('UTF-8') {
			props.store(it, 'DO NOT EDIT, generated with gradlew updateConfig')
		}
	}
}

tasks.register('updateConfig', UpdateConfigTask) {
	group = "configuration"
	description = "Generates an updated version of service.properties that includes values from 'gradle.properties' starting with 'dlb-config'."
}

class ListDialogueFilesTask extends DefaultTask {
	def listDirFiles(dir, writer) {
		writer.write('[')
		def first = true
		dir.eachFile() { child ->
			if (!child.name.startsWith('.')) {
				if (!first)
					writer.write(',')
				if (child.isDirectory()) {
					writer.write('{"' + child.name + '":')
					listDirFiles(child, writer)
					writer.write('}')
					first = false
				} else if (child.name.endsWith('.json') || child.name.endsWith('.dlb')) {
					writer.write('"' + child.name + '"')
					first = false
				}
			}
		}
		writer.write(']')
	}

	@TaskAction
	void doTaskAction() {
		def dlgDir = new File('src/main/resources/dialogues')
		def dlgFile = new File('src/main/resources/dialogues/dialogues.json')
		dlgFile.withWriter('UTF-8') { writer ->
			writer.write('{')
			def first = true
			dlgDir.eachDir() { dir ->
				if (first)
					first = false
				else
					writer.write(',')
				writer.write('"' + dir.name + '":')
				listDirFiles(dir, writer)
			}
			writer.write('}')
		}
	}
}

tasks.register('listDialogueFiles', ListDialogueFilesTask) {
	group = 'configuration'
	description = 'Lists dialogue files and writes them to dialogues.json'
}

abstract class UpdateVersionTask extends DefaultTask {
	private ProviderFactory providerFactory

	@Input
	abstract Property<String> getVersion()

	@Inject
	UpdateVersionTask(ProviderFactory providerFactory) {
		this.providerFactory = providerFactory
	}

	@TaskAction
	void doTaskAction() {
		ant.replaceregexp(file: 'src/main/webapp/WEB-INF/web.xml',
			match: "<display-name>DialogueBranch Web Service .+</display-name>",
			replace: "<display-name>DialogueBranch Web Service ${version.get()}</display-name>")
		ant.replaceregexp(file: 'src/main/resources/deployment.properties',
			match: "version=.+",
			replace: "version=${version.get()}")
	}
}

Provider<String> versionProvider = project.provider { project.version }

tasks.register('updateVersion', UpdateVersionTask) {
	group = 'versioning'
	description = 'Writes the current version to files containing the version number.'
	version = versionProvider
}

tasks.register('checkResources') {
	group = 'build'
	description = 'Checks if required resource files exist and are up-to-date.'

	dependsOn updateConfig, listDialogueFiles
}

test {
	testLogging.showStandardStreams = true
}

gradle.taskGraph.whenReady {
	tasks.withType(Test) {
		def props = new Properties()
		def propsFile = file('gradle.test.properties')
		if (propsFile.exists()) {
			propsFile.withReader('UTF-8') {
				props.load(it)
			}
			props.each { key, value ->
				systemProperty key, value
			}
		}
	}
}

javadoc {
	options {
		links 'https://docs.oracle.com/en/java/javase/17/docs/api/'
		addStringOption('Xdoclint:all,-html', '-quiet')
		addStringOption('Xmaxwarns 1000', '-quiet')
	}
}

processResources.dependsOn checkResources
